# GFX helpers that breaks compatibility with the current stable Ren'Py.

init python:
    renpy.load_module('Riopy/system/common/shaders')
    # Wave
    # TODO not tested since all ATL transitions seem broken as of gb62fb01
    def WillWave(duration, mode='vertical'):
        if mode == 'vertical':
            return WillVWaveProto(duration)
        elif mode == 'horizontal':
            return WillHWaveProto(duration)
        else:
            raise ValueError('Unknown mode {}'.format(mode))

transform WillWaveProto(xamplitude=-1, xphase=0.0, xwavelength=1.0, yamplitude=-1, yphase=0.0, ywavelength=1.0):
    shader "willplus.wave"
    u_willplus_wave_xamplitude xamplitude
    u_willplus_wave_xphase xphase
    u_willplus_wave_xwavelength xwavelength
    u_willplus_wave_yamplitude yamplitude
    u_willplus_wave_yphase yphase
    u_willplus_wave_ywavelength ywavelength

transform WillVWaveProto(duration, new_widget, old_widget):
    delay duration
    contains:
        old_widget
        mesh True
        WillWaveProto(xamplitude=0.0, xwavelength=0.25)
        parallel:
            linear (duration / 2) u_willplus_wave_xamplitude 0.25
            linear (duration / 2) u_willplus_wave_xamplitude 0.0
            u_willplus_wave_xamplitude -1
        parallel:
            linear (duration) u_willplus_wave_xphase 720
    contains:
        new_widget
        mesh True
        WillWaveProto(xamplitude=0.0, xwavelength=0.25)
        parallel:
            linear (duration / 2) u_willplus_wave_xamplitude 0.25
            linear (duration / 2) u_willplus_wave_xamplitude 0.0
            u_willplus_wave_xamplitude -1
        parallel:
            alpha 0.0
            linear (duration) u_willplus_wave_xphase 720 alpha 1.0

transform WillHWaveProto(duration, new_widget, old_widget):
    delay duration
    contains:
        old_widget
        mesh True
        WillWaveProto(yamplitude=0.0, ywavelength=0.25)
        parallel:
            linear (duration / 2) u_willplus_wave_yamplitude 0.25
            linear (duration / 2) u_willplus_wave_yamplitude 0.0
            u_willplus_wave_yamplitude -1
        parallel:
            linear (duration) u_willplus_wave_yphase 720
    contains:
        new_widget
        mesh True
        WillWaveProto(yamplitude=0.0, ywavelength=0.25)
        parallel:
            linear (duration / 2) u_willplus_wave_yamplitude 0.25
            linear (duration / 2) u_willplus_wave_yamplitude 0.0
            u_willplus_wave_yamplitude -1
        parallel:
            alpha 0.0
            linear (duration) u_willplus_wave_yphase 720 alpha 1.0

transform WillStretch(duration, new_widget, old_widget):
    delay duration
    old_widget
    mesh True
    WillWaveProto(xamplitude=0.0, xwavelength=0.001)
    linear (duration / 2) u_willplus_wave_xamplitude 0.3 u_willplus_wave_xphase 360000
    new_widget
    linear (duration / 2) u_willplus_wave_xamplitude 0.0 u_willplus_wave_xphase 0
    u_willplus_wave_xamplitude -1
